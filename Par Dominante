ClearAll["Global`*"];
colAxis = Black;
colGridA = GrayLevel[.90];
colGridB = GrayLevel[.82];
colGridC = GrayLevel[.93];
colSL = Red;

niceFont = "Times";

fsTick = 28;
fsAxisLab = 55;
fsTag = 34;


thBorder = 3.0;
thGrid = 0.8;
thRays = 1.6;
thSL = 1.8;

ptSizeBin = 14;
ptSizeMain = 16;

baseStyle = {FontFamily -> niceFont, FontSize -> fsTick};
fmt01[x_] := 
  ToString@NumberForm[x, {2, 1}, NumberPadding -> {"", "0"}];


midX2 = {0.5, 0.0};  
midX3 = {0.0, 0.5};  


offTickX3 = {-42, 0};
offTickX2 = {0, -42};
offAxisX3 = {-140, 0};
offAxisX2 = {0, -120};


(*Triángulo base {x2,x3}*)

rightBase = 
  Graphics[{EdgeForm[Directive[colAxis, AbsoluteThickness[thBorder]]],
     FaceForm[None], 
    Polygon[{{0, 1}, {0, 0}, {1, 0}}],(*ticks numéricos*)
    Table[{colAxis, 
      Text[Style[fmt01[j], baseStyle], Offset[offTickX3, {0, j}]], 
      colAxis, 
      Text[Style[fmt01[j], baseStyle], 
       Offset[offTickX2, {j, 0}]]}, {j, 0.1, 0.9, 
      0.1}],(*etiquetas x2 y x3 centradas y grandes*)
    Text[Style[Subscript["x", 3], FontFamily -> niceFont, 
      FontSize -> fsAxisLab], Offset[offAxisX3, midX3]], 
    Text[Style[Subscript["x", 2], FontFamily -> niceFont, 
      FontSize -> fsAxisLab], Offset[offAxisX2, midX2]]}, 
   BaseStyle -> baseStyle, ImageSize -> 1000];


rightGrid = 
  Graphics[{AbsoluteThickness[thGrid], 
    Table[{colGridA, Dashing[{}], Line[{{0, i}, {1 - i, i}}], 
      colGridB, Dashing[{0.02, 0.02}], Line[{{i, 0}, {i, 1 - i}}], 
      colGridC, Dashing[{}], Line[{{0, i}, {i, 0}}]}, {i, 0.1, 0.9, 
      0.1}]}];


(*Datos binarios*)

x1alpha12 = 0.0115837;
x1beta12 = 0.76801;

x1alpha13 = 0.00440884;
x1beta13 = 0.836702;

x2alpha23 = 0.35799;
x2beta23 = 0.96408;

pt12a = {1 - x1alpha12, 0};
pt12b = {1 - x1beta12, 0};

pt13a = {0, 1 - x1alpha13};
pt13b = {0, 1 - x1beta13};

pt23a = {x2alpha23, 1 - x2alpha23};
pt23b = {x2beta23, 1 - x2beta23};

bins = <|"12" -> {x1alpha12, x1beta12}, "13" -> {x1alpha13, x1beta13},
    "23" -> {x2alpha23, x2beta23}|>;


(*Rectas internas sl1,sl2,sl3*)

sl1 = {pt12a, pt13a};
sl2 = {pt12b, pt23a};
sl3 = {pt13b, pt23b};


(*Punto global Z y vértices*)

zTern = {0.4, 0.3, 0.3};   
{xz, yz} = N@{zTern[[2]], zTern[[3]]};

v1 = {0, 0}; v2 = {1, 0}; v3 = {0, 1};
eps = 1.*10^-12;


Q1 = {xz/(xz + yz + eps), yz/(xz + yz + eps)};
Q2 = {0, yz/Max[eps, 1 - xz]};
Q3 = {xz/Max[eps, 1 - yz], 0};

ClearAll[rayWithSegment];

rayWithSegment[v_, q_, a_, b_, tol_ : 1.*^-12] := 
  Module[{A = N[q - v], B = N[b - a], rhs = N[a - v], M, st, s, t}, 
   M = Transpose[{A, -B}];
   If[Abs[Det@M] < tol, Return@Missing["Parallel"]];
   st = LinearSolve[M, rhs]; s = st[[1]]; t = st[[2]];
   If[s < -tol || t < -tol || t > 1 + tol, 
    Return@Missing["NoIntersection"]];
   N[v + s A]];

P1 = rayWithSegment[v1, Q1, sl1[[1]], sl1[[2]]];
P2 = rayWithSegment[v2, Q2, sl2[[1]], sl2[[2]]];
P3 = rayWithSegment[v3, Q3, sl3[[1]], sl3[[2]]];

numPt[p_] := VectorQ[p, NumericQ] && Length[p] == 2;
Ps = Select[{P1, P2, P3}, numPt];
labelsP = Pick[{"P1", "P2", "P3"}, numPt /@ {P1, P2, P3}];

offZ = {20, 20};

offP = <|"P1" -> {28, 22}, "P2" -> {-20, 22}, "P3" -> {28, -30}|>;
offQ = <|"Q1" -> {28, -30}, "Q2" -> {28, -30}, "Q3" -> {0, -20}|>;


marks = Graphics[{(*sl_i en rojo*)
    Directive[colSL, AbsoluteThickness[thSL]], 
    Line /@ {sl1, sl2, sl3},(*rayos a Q_i*)
    Directive[colAxis, AbsoluteThickness[thRays]], 
    Line /@ {{v1, Q1}, {v2, Q2}, {v3, Q3}},(*puntos binarios*)
    Directive[colAxis, AbsolutePointSize[ptSizeBin]], 
    Point[{pt12a, pt12b, pt13a, pt13b, pt23a, pt23b}],(*Z global*)
    Directive[colAxis, AbsolutePointSize[ptSizeMain]], 
    Point[{xz, yz}], 
    Text[Style["Z", FontFamily -> niceFont, FontSize -> fsTag, Bold], 
     Offset[offZ, {xz, yz}]],(*P_i*)
    Directive[colAxis, AbsolutePointSize[ptSizeMain]], Point[Ps], 
    Map[With[{lab = #, p = Ps[[First@FirstPosition[labelsP, #]]]}, 
       Text[Style[lab, FontFamily -> niceFont, FontSize -> fsTag, 
         Bold], Offset[offP[lab], p]]] &, labelsP],(*Q_i*)
    Directive[colAxis, AbsolutePointSize[ptSizeMain]], 
    Point /@ {Q1, Q2, Q3}, {Text[
      Style["Q1", FontFamily -> niceFont, FontSize -> fsTag - 2, 
       Bold], Offset[offQ["Q1"], Q1]], 
     Text[Style["Q2", FontFamily -> niceFont, FontSize -> fsTag - 2, 
       Bold], Offset[offQ["Q2"], Q2]], 
     Text[Style["Q3", FontFamily -> niceFont, FontSize -> fsTag - 2, 
       Bold], Offset[offQ["Q3"], Q3]]}}, BaseStyle -> baseStyle];

Show[rightBase, rightGrid, marks, 
 PlotRange -> {{-0.08, 1.08}, {-0.08, 1.08}}, 
 ImagePadding -> {{170, 60}, {140, 
    70}},(*más margen para x3 y x2 grandes*)ImageSize -> 1400]


Z = {xz, yz};
dist[a_, b_] := Norm[a - b];

p1 = dist[Z, Q1]/dist[P1, Q1];
p2 = dist[Z, Q2]/dist[P2, Q2];
p3 = dist[Z, Q3]/dist[P3, Q3];

pvals = <|"p1" -> p1, "p2" -> p2, "p3" -> p3|>;
order = Ordering[{p1, p2, p3}, -2];
dominantPair = Sort[order];

pvals
dominantPair
